import java.text.SimpleDateFormat

plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
}

apply plugin: AddCurrentDatePlugin

android {
    namespace "com.kodeco.socializify"
    compileSdk libs.versions.compileSdk.get().toInteger()

    defaultConfig {
        applicationId "com.kodeco.socializify"
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()
        versionCode libs.versions.versionCode.get().toInteger()
        versionName libs.versions.versionName.get()
    }

    signingConfigs {
        release {
            storeFile file("path to your keystore file")
            storePassword "your store password"
            keyAlias "your key alias"
            keyPassword "your key password"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
        debug {
        }
    }

    flavorDimensions = ["appMode"]

    productFlavors {
        free {
            dimension "appMode"
            applicationIdSuffix ".free"
            manifestPlaceholders.appName = "@string/app_name_free"
        }
        paid {
            dimension "appMode"
            applicationIdSuffix ".paid"
            manifestPlaceholders.appName = "@string/app_name_paid"
        }
    }

    buildFeatures {
        viewBinding true
    }

    kotlin {
        jvmToolchain(17)
    }
}

abstract class AddCurrentDateTask extends DefaultTask {
    @Input
    abstract Property<String> getBuildFlavor()

    @Input
    abstract Property<String> getBuildType()

    @TaskAction
    void taskAction() {
        String oldFileName = "app-${buildFlavor.get()}-${buildType.get()}.apk"
        String sourcePath = "${project.buildDir}/outputs/apk/${buildFlavor.get()}/${buildType.get()}/"
        String date = new SimpleDateFormat("dd-MM-yyyy").format(new Date())
        project.copy {
            from(sourcePath + oldFileName)
            into(sourcePath)
            rename { date + "_" + oldFileName  }
        }
    }
}

class AddCurrentDatePlugin implements Plugin<Project> {
    @Override
    void apply(Project target) {
        target.androidComponents {
            onVariants(selector().all(), { variant ->
                TaskProvider addCurrentDateTask = target.tasks.register(
                        variant.getName() + "AddCurrentDateTask",
                        AddCurrentDateTask.class
                ) {
                    buildType.set(variant.getBuildType())
                    buildFlavor.set(variant.getFlavorName())
                }

                String capitalizedVariantName = variant.getName()
                        .substring(0, 1)
                        .toUpperCase()
                        .concat(variant.getName().substring(1))

                String assembleTaskName = "assemble$capitalizedVariantName"

                target.tasks.matching { it.name == assembleTaskName }.configureEach {
                    finalizedBy(addCurrentDateTask)
                }
            })
        }
    }
}

//androidComponents {
//    onVariants(selector().all(), { variant ->
//        TaskProvider addCurrentDateTask = project.tasks.register(
//                variant.getName() + "AddCurrentDateTask",
//                AddCurrentDateTask.class
//        ) {
//            buildType.set(variant.getBuildType())
//            buildFlavor.set(variant.getFlavorName())
//        }
//
//        String capitalizedVariantName = variant.getName()
//                .substring(0, 1)
//                .toUpperCase()
//                .concat(variant.getName().substring(1))
//
//        String assembleTaskName = "assemble$capitalizedVariantName"
//
//        project.tasks.matching { it.name == assembleTaskName }.configureEach {
//            finalizedBy(addCurrentDateTask)
//        }
//    })
//}

dependencies {
    implementation fileTree(include: ["*.jar"], dir: "libs")
    implementation libs.appcompat
    implementation libs.material
    implementation libs.picasso
}
